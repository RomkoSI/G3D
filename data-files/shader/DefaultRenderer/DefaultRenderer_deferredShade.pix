#version 410 or 420// -*- c++ -*-
/** \file DefaultRenderer_deferredShade.pix */
#ifdef GL_ARB_texture_query_lod
#   extension GL_ARB_texture_query_lod : enable
#endif
#include <compatibility.glsl>
#include <Light/Light.glsl>

#include <GBuffer/GBuffer.glsl>
// Declare a GBuffer with no prefix
uniform_GBuffer(gbuffer_);

#include <deferredHelpers.glsl>
#include <LightingEnvironment/LightingEnvironment_LightUniforms.glsl>
#include <LightingEnvironment/LightingEnvironment_environmentMapUniforms.glsl>


#ifdef ambientOcclusion_notNull
    uniform_Texture(sampler2D, ambientOcclusion_);
    uniform ivec2       ambientOcclusion_offset;
#endif
    
out Color3 result;

void main() {
    // Screen-space point being shaded
    ivec2 C = ivec2(gl_FragCoord.xy);
    
    Vector3 w_o;
    UniversalMaterialSample surfel;
    readUniversalMaterialSampleFromGBuffer(C, true, w_o, surfel);

    Radiance3 L_scatteredDirect = computeDirectLighting(surfel, w_o, 1.0);
    Radiance3 L_scatteredIndirect = computeIndirectLighting(surfel, w_o, false, 0);

    float AO =
#       ifdef ambientOcclusion_notNull
            (0.95 * texelFetch(ambientOcclusion_buffer, min(ivec2(gl_FragCoord.xy) + ambientOcclusion_offset, ivec2(ambientOcclusion_size.xy) - ivec2(1, 1)), 0).r + 0.05);
#       else
            1.0;
#       endif

    // How much ambient occlusion to apply to direct illumination (sort of approximates area lights,
    // more importantly: NPR term that adds local contrast)
    const float aoInfluenceOnDirectIllumination = 0.65;
    float directAO = lerp(1.0, AO, aoInfluenceOnDirectIllumination);

    result = surfel.emissive + L_scatteredDirect * directAO + L_scatteredIndirect * AO;
}
