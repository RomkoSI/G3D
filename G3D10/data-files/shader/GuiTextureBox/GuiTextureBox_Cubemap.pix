#version 330

#expect SAMPLER_TYPE "gsamplerXX"

/**
  \file GuiTextureBox_Cubemap.pix
  \author Morgan McGuire, http://graphics.cs.williams.edu, Michael Mara
 */
uniform SAMPLER_TYPE tex_buffer;
uniform float     adjustGamma;
uniform mat4      colorShift;
uniform float     bias;
uniform float     scale;
uniform float     edgeThreshold;
uniform bool      invertIntensity;
    
in vec2 g3d_TexCoord;

out vec4 result;

void main(void) {
    const float PI    = 3.14159265;
    vec2 sphericalCoord = g3d_TexCoord.xy;
    float theta = sphericalCoord.y * PI;
    float phi   = sphericalCoord.x * 2 * PI;
    float sinTheta = sin(theta);
    vec3 cartesianCoord = vec3(cos(phi) * sinTheta, cos(theta), sin(phi) * sinTheta); 
    vec4 c = textureLod(tex_buffer, cartesianCoord, 0.0);
    c = (c + bias) * scale;
    c = invertIntensity ? vec4(1.0 - c) : c;
    c = colorShift * c;
    c = max(c, vec4(0.0));

    vec3 cubeMapColor = pow(c.rgb, vec3(adjustGamma));
    vec3 edgeColor = vec3( 0.196, 0.804, 0.196); // Lime Green

                                 
    vec3 v = abs(cartesianCoord);
    v = v / max(v.x, max(v.y, v.z));
    bool onEdge = (v.x + v.y + v.z - min(v.x,min(v.y,v.z))) > edgeThreshold;
    result.rgb = onEdge ? edgeColor : cubeMapColor;
    result.a = 1.0;
}