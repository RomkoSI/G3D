#version 330 // -*- c++ -*-

/** 
  \file AmbientOcclusion_blur.pix
  \author Morgan McGuire and Michael Mara, NVIDIA Research

  \brief 7-tap 1D cross-bilateral blur using a packed depth key
  
  Open Source under the "BSD" license: http://www.opensource.org/licenses/bsd-license.php

  Copyright (c) 2011-2014, NVIDIA
  All rights reserved.

  Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

  Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
  Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

#expect PEELED_LAYER "binary"

//////////////////////////////////////////////////////////////////////////////////////////////
// Tunable Parameters:

#define NUM_KEY_COMPONENTS 1

// The other parameters in this section must be passed in as macro values

/** Increase to make depth edges crisper. Decrease to reduce flicker. */
//#define EDGE_SHARPNESS     (1.0)

/** Step in 2-pixel intervals since we already blurred against neighbors in the
    first AO pass.  This constant can be increased while R decreases to improve
    performance at the expense of some dithering artifacts. 
    
    Morgan found that a scale of 3 left a 1-pixel checkerboard grid that was
    unobjectionable after shading was applied but eliminated most temporal incoherence
    from using small numbers of sample taps.
    */
//#define SCALE               (2)

/** Filter radius in pixels. This will be multiplied by SCALE. */
//#define R                   (4)


//////////////////////////////////////////////////////////////////////////////////////////////

/** Type of data to read from source.  This macro allows
    the same blur shader to be used on different kinds of input data. */
#define VALUE_TYPE        vec4

/** Swizzle to use to extract the channels of source. This macro allows
    the same blur shader to be used on different kinds of input data. */
#define VALUE_COMPONENTS   rgba

#define VALUE_IS_KEY       0

uniform sampler2D   source;

uniform sampler2D   cszBuffer;

/** (1, 0) or (0, 1)*/
uniform ivec2       axis;

#include <compatibility.glsl>


out vec4 result;


#if USE_OCT16
    #include <oct.glsl>
#endif

vec3 sampleNormal(sampler2D normalBuffer, ivec2 ssC, int mipLevel) {
#if USE_OCT16
    return decode16(texelFetch(normalBuffer, ssC, mipLevel).xy * 2.0 - 1.0);
#else
    return normalize(texelFetch(normalBuffer, ssC, mipLevel).xyz * 2.0 - 1.0);
#endif
}


/** Used for preventing AO computation on the sky (at infinite depth) and defining the CS Z to bilateral depth key scaling. 
    This need not match the real far plane. Should match the value in AmbientOcclusion_AO.pix */
#       define FAR_PLANE_Z (-300.0)
#       include <reconstructFromDepth.glsl>
        uniform vec4        projInfo;

vec3 positionFromKey(float key, ivec2 ssC, vec4 pInfo) {
    float z = key * FAR_PLANE_Z;
    vec3 C = reconstructCSPosition(vec2(ssC) + vec2(0.5), z, pInfo);
    return C;        
}

#ifdef normal_notNull
    /** Same size as result buffer, do not offset by guard band when reading from it */
    uniform sampler2D       normal_buffer;
    uniform vec4            normal_readMultiplyFirst; // 2.0
    uniform vec4            normal_readAddSecond; // 1.0
#endif

float calculateBilateralWeight(float key, float tapKey, ivec2 tapLoc, vec3 n_C, vec3 C) {
    // range domain (the "bilateral" weight). As depth difference increases, decrease weight.
    float depthWeight = max(0.0, 1.0
            - (EDGE_SHARPNESS * 2000.0) * abs(tapKey - key)
            );

    float k_normal = 40.0;
    float k_plane = 0.5;

    // Prevents blending over creases. 
    float normalWeight = 1000.0;
    float planeWeight = 1.0;

#   ifdef normal_notNull
        vec3 tapN_C = sampleNormal(normal_buffer, tapLoc, 0);
        depthWeight = 1.0;

        float normalError = (1.0 - dot(tapN_C, n_C)) * k_normal;
        normalWeight = max(1.0 - EDGE_SHARPNESS*normalError, 0.00);
        

        float lowDistanceThreshold2 = 0.01;

        vec3 tapC = positionFromKey(tapKey, tapLoc, projInfo);

        // Change in position in camera space
        vec3 dq = C - tapC;
                
        // How far away is this point from the original sample
        // in camera space? (Max value is unbounded)
        float distance2 = dot(dq, dq);
                
        // How far off the expected plane (on the perpendicular) is this point?  Max value is unbounded.
        float planeError = max(abs(dot(dq, tapN_C)), abs(dot(dq, n_C)));

        planeWeight = (distance2 < lowDistanceThreshold2) ? 1.0 :
                         pow(max(0.0, 1.0 - EDGE_SHARPNESS * 2.0 * k_plane * planeError / sqrt(distance2)), 2.0);


#   endif

    return depthWeight * normalWeight * planeWeight;
}

float getKey(ivec2 ssP) {
#   if PEELED_LAYER
        float key = texelFetch(cszBuffer, ssP, 0).g;
#   else 
        float key = texelFetch(cszBuffer, ssP, 0).r;
#   endif
    key = clamp(key * (1.0 / FAR_PLANE_Z), 0.0, 1.0);
    return key;
}
void main() {

        float kernel[R + 1];
//      if R == 0, we never call this shader
#       if R == 1 
            kernel[0] = 0.5; kernel[1] = 0.25;
#       elif R == 2 
            kernel[0] = 0.153170; kernel[1] = 0.144893; kernel[2] = 0.122649;
#       elif R == 3 
            kernel[0] = 0.153170; kernel[1] = 0.144893; kernel[2] = 0.122649; kernel[3] = 0.092902;
#       elif R == 4 
            kernel[0] = 0.153170; kernel[1] = 0.144893; kernel[2] = 0.122649; kernel[3] = 0.092902; kernel[4] = 0.062970;
#       elif R == 5 
            kernel[0] = 0.111220; kernel[1] = 0.107798; kernel[2] = 0.098151; kernel[3] = 0.083953; kernel[4] = 0.067458; kernel[5] = 0.050920;
#       elif R == 6
            kernel[0] = 0.111220; kernel[1] = 0.107798; kernel[2] = 0.098151; kernel[3] = 0.083953; kernel[4] = 0.067458; kernel[5] = 0.050920; kernel[6] = 0.036108;
#       endif


    ivec2 ssC = ivec2(gl_FragCoord.xy);

    vec4 temp = texelFetch(source, ssC, 0);

    float key = getKey(ssC);

    VALUE_TYPE sum = temp.VALUE_COMPONENTS;

    if (key == 1.0) { 
        // Sky pixel (if you aren't using depth keying, disable this test)
        result = sum;
        return;
    }

    // Base weight for falloff.  Increase this for more blurriness,
    // decrease it for better edge discrimination
    float BASE = kernel[0];
    float totalWeight = BASE;
    sum *= totalWeight;

    vec3 n_C;
#   ifdef normal_notNull
        n_C = sampleNormal(normal_buffer, ssC, 0);
#   endif

    vec3 C = positionFromKey(key, ssC, projInfo);

# if MDB_WEIGHTS==0
    for (int r = -R; r <= R; ++r) {
        // We already handled the zero case above.  This loop should be unrolled and the static branch optimized out,
        // so the IF statement has no runtime cost
        if (r != 0) {
            ivec2 tapLoc = ssC + axis * (r * SCALE);
            temp = texelFetch(source, tapLoc, 0);
            

            float tapKey = getKey(tapLoc);
            VALUE_TYPE value  = temp.VALUE_COMPONENTS;
            
            // spatial domain: offset kernel tap
            float weight = 0.3 + kernel[abs(r)];   

            float bilateralWeight = calculateBilateralWeight(key, tapKey, tapLoc, n_C, C);

            weight *= bilateralWeight;
            sum += value * weight;
            totalWeight += weight;
        }
    }
#else 

    float lastBilateralWeight = 9999.0;
    for (int r = -1; r >= -R; --r) {
        ivec2 tapLoc = ssC + axis * (r * SCALE);
        temp = texelFetch(source, tapLoc, 0);
        float      tapKey = getKey(tapLoc);
     
        VALUE_TYPE value  = temp.VALUE_COMPONENTS;
            
        // spatial domain: offset kernel tap
        float weight = 0.3 + kernel[abs(r)];

        // range domain (the "bilateral" weight). As depth difference increases, decrease weight.
        float bilateralWeight = calculateBilateralWeight(key, tapKey, tapLoc, n_C, C);
        bilateralWeight = min(lastBilateralWeight, bilateralWeight);
        lastBilateralWeight = bilateralWeight;
        weight *= bilateralWeight;
        sum += value * weight;
        totalWeight += weight;
    }

    lastBilateralWeight = 9999.0;
    for (int r = 1; r <= R; ++r) {
        ivec2 tapLoc = ssC + axis * (r * SCALE);
        temp = texelFetch(source, tapLoc, 0);
        float      tapKey = getKey(tapLoc);
        VALUE_TYPE value  = temp.VALUE_COMPONENTS;
            
        // spatial domain: offset kernel tap
        float weight = 0.3 + kernel[abs(r)];

        // range domain (the "bilateral" weight). As depth difference increases, decrease weight.
        float bilateralWeight = calculateBilateralWeight(key, tapKey, tapLoc, n_C, C);
        bilateralWeight = min(lastBilateralWeight, bilateralWeight);
        lastBilateralWeight = bilateralWeight;
        weight *= bilateralWeight;
        sum += value * weight;
        totalWeight += weight;
    }
#endif
 
    const float epsilon = 0.0001;

    result = sum / (totalWeight + epsilon);	
}
